plugins {
    id 'java'
    id 'idea'
    id 'jacoco'
    id 'com.github.kt3k.coveralls' version '2.6.3'
}

apply plugin: "com.jfrog.artifactory"
apply plugin: "maven-publish"
apply plugin: 'groovy'

sourceCompatibility = '1.7'
targetCompatibility = '1.7'

sourceSets {
    test {
        resources {
            srcDir "resources"
            includes["**/*.json"]
        }
    }
}

dependencies {
    implementation 'com.squareup.moshi:moshi:1.4.0'
    testImplementation 'com.squareup.moshi:moshi:1.4.0'
    testImplementation 'junit:junit:4.12'
}

test {
    testLogging {
        events "passed", "skipped", "failed", "standardOut", "standardError"
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

jacocoTestReport {
    reports {
        xml.enabled = true // coveralls plugin depends on xml format report
        html.enabled = true
    }
}

apply from: "${rootDir}/buildsystem/artifactory.gradle"

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId "moshi-jsonapi-core"
            artifact(file("$buildDir/libs/core.jar"))
        }
    }
}

